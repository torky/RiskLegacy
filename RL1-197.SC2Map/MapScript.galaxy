//Must be first
include "TriggerLibs/NativeLib"

string gameMode;

const string c_mode1v1 = "0007";
const string c_modeCustom = "0002";
const string c_modeEmpire = "0006";
const string c_modeQuick = "0001";
const string c_modeSolo = "0004";
const string c_modeTeams = "0005";
const string c_modeTutorial = "0003";
const string c_modeKingOfTheHill = "0008";

//Level 0
include "scripts/players/AdminData"
include "scripts/ui/Buttons"
include "scripts/flags/Flags"
include "scripts/ui/MenuTrigger"
include "scripts/sort/MergeSort"
include "scripts/players/Players"
include "scripts/skins/Skins"
include "scripts/start/Start"
include "scripts/units/UnitTrained"

//Level 1
include "scripts/units/Units"
include "scripts/skins/data/SkinsData"

//Level 2
include "scripts/bases/Bases"
include "scripts/bounty/Bounty"
include "scripts/flags/FlagVisibility"
include "scripts/stats/Stats"

//Level 3
include "scripts/banks/Banks"
include "scripts/cards/Cards"
include "scripts/countries/Country"
include "scripts/map/Map"
include "scripts/ui/Scoreboard"
include "scripts/settings/SettingsController"
include "scripts/skins/SkinsController"
include "scripts/victoryPercentage/VictoryPercentage"

//Level 4
include "scripts/bases/BaseCapture"
include "scripts/countries/Conscripts"
include "scripts/flags/HomelandFlags"
include "scripts/ui/UI"

//Level 5
include "scripts/players/AdminCommands"
include "scripts/bases/DefenderAbility"
include "scripts/bases/DistributeTerritories" 
include "scripts/income/Income"
include "scripts/countries/PredefineCountries"

//Level 6
include "scripts/refresh/Refresh"

//Level 7
include "scripts/modes/1v1"
include "scripts/modes/Custom"
include "scripts/modes/Empire"
include "scripts/modes/KingOfTheHill"
include "scripts/modes/Quick"
include "scripts/modes/Solo"
include "scripts/modes/Teams"
include "scripts/modes/Tutorial"
include "scripts/chat/Chat"
include "scripts/ui/ButtonPressed"
include "scripts/players/PlayerLeaves"

void InitLibs () {
    libNtve_InitLib();
}

trigger gt_GameModeSelect;

bool gt_GameModeSelect_Func (bool testConds, bool runActions) {
    playergroup players;
    funcref<gf_SetBaseOwnership> baseOwnershipFunc = SetBaseOwnership;
    
    gameMode = GameAttributeGameValue("1");
    VisEnable(c_visTypeMask, true);
    VisEnable(c_visTypeFog, true);
    DeleteStupidRocks();
    DeclareSkinTypes();
    LoadPlayerAdmins();
    SetInGamePlayerData();
    LoadBankInfoForPlayers();
    UpdateScoreStatsForAllPlayers();
    PredefineCountries();
    InitializeBases();
    
    if (gameMode == c_mode1v1) {
        UIDisplayMessage(PlayerGroupActive(), c_messageAreaChat, StringToText("1v1"));
        baseOwnershipFunc = SetBaseOwnership1v1;
    }else if (gameMode == c_modeCustom) {
        UIDisplayMessage(PlayerGroupActive(), c_messageAreaChat, StringToText("Custom"));

    }else if (gameMode == c_modeEmpire) {
        UIDisplayMessage(PlayerGroupActive(), c_messageAreaChat, StringToText("Empire"));

    }else if (gameMode == c_modeQuick) {
        UIDisplayMessage(PlayerGroupActive(), c_messageAreaChat, StringToText("Quick"));
        QuickModeSetup();
    }else if (gameMode == c_modeSolo) {
        UIDisplayMessage(PlayerGroupActive(), c_messageAreaChat, StringToText("Solo"));

    }else if (gameMode == c_modeTeams) {
        UIDisplayMessage(PlayerGroupActive(), c_messageAreaChat, StringToText("Teams"));
        
    }else if (gameMode == c_modeTutorial) {
        UIDisplayMessage(PlayerGroupActive(), c_messageAreaChat, StringToText("Tutorial"));

    }else if (gameMode == c_modeKingOfTheHill){
        UIDisplayMessage(PlayerGroupActive(), c_messageAreaChat, StringToText("King of the Hill"));
        KOTHHookUp();
        RandomizeOrder();
    }
    
    players = PlayerGroupActive();
    DistributeTerritories(players, baseOwnershipFunc);
    UpdateCountries();
    UpdatePlayerSpawnControlGroups();
    ShowHideFlagsForAllPlayers();
    
    SetupUI();
    CreateButtons();

    VisEnable(c_visTypeMask, false);
    VisEnable(c_visTypeFog, false);
    StartRefresh();

    return true;
}

void gt_GameModeSelect_Init () {
    gt_GameModeSelect = TriggerCreate("gt_GameModeSelect_Func");
    TriggerAddEventMapInit(gt_GameModeSelect);
}

void InitTriggers () {
    gt_Menu_Init();
    gt_UnitTrained_Init();
    gt_SetupPlayers_Init();
    gt_Bounty_Init();
    gt_SwapDefender_Init();
    gt_BaseCapture_Init();
    gt_EmptyBaseCapture_Init();
    gt_GameModeSelect_Init();
    gt_PlayerAdminCommands_Init();
    gt_SetHomeland_Init();
    gt_SpawnConscripts_Init();
    gt_ConscriptSpawned_Init();
    gt_ShowHideMapHistory_Init();
    gt_ViewTurn_Init();
    gt_Refresh_Init();
    gt_PlayerLeaves_Init();
    gt_Chat_Init();
    gt_ButtonPressed_Init();
}

void InitMap () {
    InitLibs();
    InitTriggers();
}


