trigger gt_Refresh;

int totalTurnTime = 10;
int remainingTime = 5;
int turns = 0;

bool Refresh(bool testConds, bool runActions){
    int count;
    int i;
    int player;
    playergroup players;

    players = PlayerGroupAll();
    count = PlayerGroupCount(players);

    //Decrement turn time
    remainingTime-=1;
    if(remainingTime < 1){
        //Increment turns
        RefreshMapHistory(turns);
        turns+=1;
        remainingTime=totalTurnTime;
        RefreshIncome();
        RefreshConscripts();
    }else if (remainingTime < 4) {
        SoundPlay(SoundLink("UI_BnetCountdown", -1), players, 100.0, 0.0);
    }

    for(i = 1; i <= PlayerGroupCount(players); i += 1){
        player = PlayerGroupPlayer(players, i);
        PlayerModifyPropertyInt(player, c_playerPropTerrazine, c_playerPropOperSetTo, remainingTime);
        PlayerModifyPropertyInt(player, c_playerPropCustom, c_playerPropOperSetTo, turns);
    }

    return true;
}

void gt_Refresh_Init(){
    gt_Refresh = TriggerCreate("Refresh");
    TriggerAddEventTimePeriodic(gt_Refresh, 1.0, c_timeGame);
    TriggerEnable(gt_Refresh, false);
}

void StartRefresh(){
    TriggerEnable(gt_Refresh, true);
}

void StopRefresh(){
    TriggerEnable(gt_Refresh, false);
}