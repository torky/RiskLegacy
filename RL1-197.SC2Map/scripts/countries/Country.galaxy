const int maxBasesInCountry = 10; //Can be changed

struct Country {
    string name;
    unitgroup structures;
    int conscriptsSpawned;
    int conscriptSpawnLimit;
    int conscriptsSpawnedPerTurn;
    unitgroup conscripts;
    int[maxBasesInCountry] baseIDs;
    unit spawn;
    string flagTexture;
    string flagImage;
    int income;
    int owner;
};

const int totalCountries = 100; //100 is arbitrary. Just needs to be larger than the number of total countries
Country[totalCountries] allCountries;

void AddTextTags(int i){
    if(allCountries[i].income == 0) { return; }
    TextTagCreate(StringToText((allCountries[i].name + (": " + IntToString(allCountries[i].income)))), 25, UnitGetPosition(allCountries[i].spawn), 0.5, true, false, PlayerGroupAll());
    TextTagSetColor(TextTagLastCreated(), c_textTagColorText, Color(100.00, 100.00, 100.00));
}

void UpdateCountry(int countryID){
    structref<Country> country;
    int i;
    int newOwner;
    int oldOwner;
    int baseOwner;
    structref<Base> base;

    country = allCountries[countryID];
    oldOwner = country.owner;
    base = allBases[country.baseIDs[0]];
    newOwner = UnitGetOwner(base.structure);

    for(i = 1; i < country.income; i+=1){
        base = allBases[country.baseIDs[i]];
        baseOwner = UnitGetOwner(base.structure);
        if(baseOwner != newOwner){
            country.owner = 0;
            return;
        }
    }

    if(newOwner != oldOwner){
        country.conscripts = UnitGroupEmpty();
        UnitIssueOrder(country.spawn, OrderTargetingUnit(AbilityCommand("Rally", 0), country.spawn), c_orderQueueReplace);
    }
    
    country.owner = newOwner;
    UnitSetOwner(country.spawn, newOwner, true);
}

void UpdateCountries(){
    int i;
    
    for(i = 0; i < totalCountries; i+=1){
        UpdateCountry(i);
    }
}

void InitializeBases () {
    int i;
    int count;
    int baseID = 0;
    int y;
    unit u;
    structref<Base> b;
    for (i = 0; i < totalCountries; i+=1) {
        count = allCountries[i].income;
        y = 0;
        for(;;count-=1){
            u = UnitGroupUnitFromEnd(allCountries[i].structures, count);
            if(u == null) {break;}
            InitializeBase(0, baseID, u);

            allCountries[i].baseIDs[y] = baseID;
            SetCountryIDOfUnit(allBases[baseID].defender, i);

            baseID+=1;
            y+=1;
        }
    }
}

